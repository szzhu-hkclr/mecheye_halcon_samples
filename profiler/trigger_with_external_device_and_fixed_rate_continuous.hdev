<?xml version="1.0" encoding="UTF-8"?>
<hdevelop file_version="1.2" halcon_version="20.11.1.2">
<procedure name="main">
<interface/>
<body>
<c>*</c>
<c>* With this sample, you can perform rounds of data acquisition continously and obtain the profile data (including intensity and depth data).</c>
<c>* In this sample, each round of data acquisition is triggered by the externally input signals, and the scanning of each line is triggered at a fixed rate.</c>
<c>* Attention: This sample is not applicable to LNX-8080A.</c>
<c>*</c>
<c></c>
<c>* Discover all available laser profilers and store them in the "MechEyeProfilerInfos" variable.</c>
<c>* You can replace "LNX" with the "user_name" or "unique_name" of the laser profiler to be connected.</c>
<l>DeviceInfo := 'LNX'</l>
<l>info_framegrabber ('GigEVision2', 'device', Info, DeviceInfos)</l>
<l>tuple_regexp_select(DeviceInfos, DeviceInfo, MechEyeProfilerInfos)</l>
<l>dev_inspect_ctrl (MechEyeProfilerInfos)</l>
<c>* Stop execution if no laser profilers are found.</c>
<l>if (not |MechEyeProfilerInfos|)</l>
<l>    stop()</l>
<l>endif</l>
<c></c>
<c>* Connect to the laser profiler: if "user_name" or "unique_name" is not set in "DeviceInfo", the first laser profiler in "MechEyeProfilersInfos" will be connected. </c>
<l>open_framegrabber ('GigEVision2', 0, 0, 0, 0, 0, 0, 'default', -1, 'default', -1, 'false', 'default', MechEyeProfilerInfos[0], 0, -1, AcqHandle)</l>
<c></c>
<c>* Obtain the available parameter names and firmware version of the laser profiler.</c>
<l>get_framegrabber_param (AcqHandle, 'available_param_names', ParameterValues)</l>
<l>get_framegrabber_param (AcqHandle, 'DeviceFirmwareVersion', FirmwareVersion)</l>
<c>* Stop execution if the firmware version is lower than 2.2.0.</c>
<l>if (FirmwareVersion &lt; '2.2.0')</l>
<l>    stop()</l>
<l>endif</l>
<c>* Set the "grab_timeout" parameter.</c>
<c>* The "grab_timeout" parameter defines the maximum time in milliseconds that the frame grabber will wait for an image to be grabbed before timing out.</c>
<l>set_framegrabber_param (AcqHandle, 'grab_timeout', 60000)</l>
<c>* Set the maximum number of packet gaps allowed in the GigE Vision stream. </c>
<c>* A packet gap occurs when one or more packets are missing during the data transmission. </c>
<c>* Setting this parameter to 500 allows a maximum of 500 packet gaps in the stream.</c>
<l>set_framegrabber_param (AcqHandle, '[Stream]GevStreamMaxPacketGaps', 500)</l>
<c>* Set the "DeviceScanType" parameter to "Areascan" to obtain the raw image.</c>
<l>set_framegrabber_param (AcqHandle, 'DeviceScanType', 'Areascan')</l>
<c>* Set the "ExposureMode" parameter to "Timed" and the "ExposureTime" parameter to 80 Î¼s.</c>
<l>set_framegrabber_param (AcqHandle, 'ExposureMode', 'Timed')</l>
<l>set_framegrabber_param (AcqHandle, 'ExposureTime', 80)</l>
<c>* Set the "ComponentSelector" parameter to "Intensity" and the "PixelFormat" parameter to "Mono8" to obtain the raw image.</c>
<l>set_framegrabber_param (AcqHandle, 'ComponentSelector', 'Intensity')</l>
<l>set_framegrabber_param (AcqHandle, 'PixelFormat', 'Mono8')</l>
<c>* Obtain the raw image from the laser profiler.</c>
<l>grab_image (RawImage, AcqHandle)</l>
<c>* Save the raw image to the "RawImage" BMP file.</c>
<l>write_image( RawImage , 'bmp' , 0 , 'RawImage' )</l>
<c></c>
<c>* Set the "DeviceScanType" parameter to "Linescan3D" to obtain the profile data.</c>
<c>* If the laser profiler has not fully completed the data acquisition, the "DeviceScanType" parameter may be locked.</c>
<c>* Therefore, a for loop with wait is used to make up to 5 attempts at setting the parameter value.</c>
<l>for Index :=1 to 5 by 1 </l>
<l>    wait_seconds(5)</l>
<l>    try</l>
<l>        set_framegrabber_param (AcqHandle, 'DeviceScanType', 'Linescan3D')</l>
<l>        break</l>
<l>    catch (Exception)</l>
<l>        continue</l>
<l>    endtry </l>
<l>endfor</l>
<c>* Stop execution if all 5 attempts failed.</c>
<l>if (Index == 5)</l>
<l>    stop()</l>
<l>endif</l>
<c>* Set the "create_objectmodel3d" parameter to "enable" to create a 3D object model from the acquired profile data.</c>
<l>set_framegrabber_param (AcqHandle, 'create_objectmodel3d', 'enable')</l>
<c>* Set the "add_objectmodel3d_overlay_attrib" parameter to "enable" to add an overlay to the 3D object model using the intensity data.</c>
<l>set_framegrabber_param (AcqHandle, 'add_objectmodel3d_overlay_attrib', 'enable')</l>
<c>* Obtain the available options of the "ComponentSelector" parameter.</c>
<l>get_framegrabber_param (AcqHandle, 'ComponentSelector_values', componentSelectorValues)</l>
<c>* Set the "Height" of image to 1600, which is equal to the number of lines to be scanned.</c>
<l>set_framegrabber_param (AcqHandle, 'Height', 1600)</l>
<c>* Get the "Width" and "Height" of image.</c>
<l>get_framegrabber_param (AcqHandle, 'Width', Width)</l>
<l>get_framegrabber_param (AcqHandle, 'Height', Height)</l>
<c>* Set the "ComponentSelector" parameter to "Reflectance" and the "PixelFormat" parameter to "Mono8" to enable the acquisition of the intensity data. </c>
<l>set_framegrabber_param (AcqHandle, 'ComponentSelector', 'Reflectance')</l>
<l>set_framegrabber_param (AcqHandle, 'PixelFormat', 'Mono8')</l>
<c>* Set the "ComponentSelector" parameter to "Range" and the "PixelFormat" parameter to "Coord3D_C16" to enable the acquisition of the depth data.</c>
<l>set_framegrabber_param (AcqHandle, 'ComponentSelector', 'Range')</l>
<l>set_framegrabber_param (AcqHandle, 'PixelFormat', 'Coord3D_C16')</l>
<c>* Set the "DataAcquisitionTriggerSource" parameter to "External", so that data acquisition is triggered by the externally input signals.</c>
<l>set_framegrabber_param (AcqHandle, 'DataAcquisitionTriggerSource', 'External')</l>
<c>* Set the "AcquisitionMode" parameter to "Continuous", so that the laser profiler continuously performs rounds of data acquisition, and the sample obtains the intensity and depth data from each round.</c>
<l>set_framegrabber_param (AcqHandle, 'AcquisitionMode', 'Continuous')</l>
<c>* Set the "LineScanTriggerSource" parameter to "FixedRate", so that the scanning of each line is triggered at a fixed rate.</c>
<c>* The fixed rate is set through the "AcquisitionLineRate" parameter.</c>
<l>set_framegrabber_param (AcqHandle, 'LineScanTriggerSource', 'FixedRate')</l>
<c>* Set the "AcquisitionLineRate" parameter to 2000 Hz.</c>
<l>set_framegrabber_param (AcqHandle, 'AcquisitionLineRate', 2000) </l>
<c></c>
<c>* Start the continuous data acquisition process.</c>
<c>* Attention: "grab_image_start()" must be called before "grab_data_async()" to start the data acquisition. </c>
<c>* Attention: HALCON discourages the use of "set_framegrabber_param (AcqHandle, 'AcquisitionStart', 1)", as this parameter is handled internally by HALCON.</c>
<l>grab_image_start(AcqHandle, -1)</l>
<c>* Use a for loop to obtain the data from two rounds of data acquisition by the laser profiler and process the data.</c>
<c>* Make sure that the external device sends a data acquisition trigger signal to the laser profiler after "grab_data_async()" is called and before the set "grab_timeout" period has passed.</c>
<l>for Index :=1 to 2 by 1 </l>
<l>    try</l>
<c>        * Asynchronously obtain the data from the laser profiler and store it to the provided variables. </c>
<l>        grab_data_async(Image, Region, Contours, AcqHandle, -1, ObjectModel3D)</l>
<l>    catch (Exception)</l>
<l>        continue</l>
<l>    endtry </l>
<c>    * Select the "Reflectance" component (intensity data) from the acquired data and store it to the variable "reflectance". </c>
<l>    select_obj (Image, reflectance, 1)</l>
<c>    * Get the width and height of the intensity image and store the values in the variables "reflectanceWidth" and "reflectanceHeight".</c>
<l>    get_image_size (reflectance, reflectanceWidth, reflectanceHeight)</l>
<c>    * Select the "Range" component (depth data) from the acquired data and store it to the variable "depthRaw".</c>
<l>    select_obj(Image, depthRaw, 2)</l>
<c>    * Set the "ChunkComponentSelector" parameter to "Range" to get the scaling factor and offset for the Z (depth) values. </c>
<l>    set_framegrabber_param (AcqHandle, 'ChunkComponentSelector', 'Range')</l>
<l>    get_framegrabber_param (AcqHandle, 'ChunkScan3dCoordinateScale', scaleZ) </l>
<l>    get_framegrabber_param (AcqHandle, 'ChunkScan3dCoordinateOffset', offsetZ)</l>
<c>    * Transform the Z values using the scaling factor and offset.</c>
<l>    convert_image_type(depthRaw, depthReal, 'real')</l>
<c>    * Filter out the points with invalid depth values.</c>
<l>    threshold(depthReal, validRegion, 1e-30, 'max')</l>
<l>    reduce_domain(depthReal, validRegion, depthValid) </l>
<c>    * Scale the depth map using the "scaleZ" and "offsetZ" values, and store the scaled result to the variable "depthInMM".</c>
<l>    scale_image(depthValid, depthInMM, scaleZ, offsetZ)</l>
<c>    * Clear the 3D object model stored in the variable "ObjectModel3D", preparing it for further processing or storage of new data. </c>
<l>    clear_object_model_3d (ObjectModel3D)</l>
<l>endfor</l>
<c></c>
<c>* Close the frame grabber.</c>
<l>close_framegrabber (AcqHandle)</l>
<c></c>
</body>
<docu id="main">
<parameters/>
</docu>
</procedure>
</hdevelop>
